buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RELEASE")
    }
}

configure(rootProject) {
    task clean {
        doLast {
            ant.delete(dir: "build")
        }
    }
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    version =  '0.1.0'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/libs-milestone" }
        mavenLocal()
    }

    dependencies {
        compile("org.springframework.data:spring-yarn-boot:2.0.0.BUILD-SNAPSHOT")
    }

    jar {
        baseName = 'gs-yarn-uimodel'
    }

    task copyJars(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/build/libs"
        include "**/*.jar"
    }
    build.doLast {
        copyJars.execute()
    }

}

project('client') {
    eclipse.project.name = 'gs-yarn-uimodel-client'
    dependencies {
        compile("args4j:args4j:2.0.16")
    }
    jar {
        appendix = 'client'
    }
}

project('appmaster') {
    eclipse.project.name = 'gs-yarn-uimodel-appmaster'
    configurations.all {
        exclude group: 'javax.servlet', module: 'servlet-api', version: '2.5'
    }
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:1.0.0.RELEASE") {
            exclude group:'ch.qos.logback'
        }
        compile("org.springframework.boot:spring-boot-starter-actuator:1.0.0.RELEASE") {
            exclude group:'ch.qos.logback'
        }
        compile("com.fasterxml.jackson.core:jackson-databind")
        compile("org.springframework.hateoas:spring-hateoas:0.8.0.RELEASE")

        //testCompile("org.springframework.data:spring-yarn-boot:2.0.0.BUILD-SNAPSHOT") {
        //    exclude group: "javax.servlet", module: "servlet-api"
        //}

        testCompile("org.mockito:mockito-core:1.9.5") { dep ->
            exclude group: "org.hamcrest"
        }
        testCompile "org.springframework:spring-test:4.0.3.RELEASE"
        testCompile "org.hamcrest:hamcrest-core:1.3"
        testCompile "org.hamcrest:hamcrest-library:1.3"
        testCompile "junit:junit:4.11"
        //testCompile "javax.servlet:servlet-api:3.0.1"
    }
    jar {
        appendix = 'appmaster'
    }
}

project('container') {
    eclipse.project.name = 'gs-yarn-uimodel-container'
    jar {
        appendix = 'container'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
