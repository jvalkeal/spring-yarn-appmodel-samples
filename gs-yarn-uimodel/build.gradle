buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
    }
}

configure(rootProject) {
    task clean {
        doLast {
            ant.delete(dir: "build")
        }
    }
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    version =  '0.1.0'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-milestone" }
    }

    dependencies {
        compile("org.springframework.data:spring-yarn-boot:2.2.0.RELEASE")
    }

    jar {
        baseName = 'gs-yarn-uimodel'
    }

    task copyJars(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/build/libs"
        include "**/*.jar"
    }
    build.doLast {
        copyJars.execute()
    }

}

project('client') {
    eclipse.project.name = 'gs-yarn-uimodel-client'
    dependencies {
        compile("org.springframework.data:spring-yarn-boot-cli:2.2.0.RELEASE")
    }
    jar {
        appendix = 'client'
    }
}

project('appmaster') {
    eclipse.project.name = 'gs-yarn-uimodel-appmaster'
    configurations.all {
        exclude group: 'javax.servlet', module: 'servlet-api', version: '2.5'
    }
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:1.2.3.RELEASE") {
            exclude group:'ch.qos.logback'
        }
        compile("org.springframework.boot:spring-boot-starter-actuator:1.2.3.RELEASE") {
            exclude group:'ch.qos.logback'
        }
        runtime("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")
    }
    jar {
        appendix = 'appmaster'
    }
}

project('cont1') {
    eclipse.project.name = 'gs-yarn-uimodel-cont1'
    jar {
        appendix = 'cont1'
    }
}

project('cont2') {
    eclipse.project.name = 'gs-yarn-uimodel-cont2'
    jar {
        appendix = 'cont2'
    }
}

project('cont3') {
    eclipse.project.name = 'gs-yarn-uimodel-cont3'
    jar {
        appendix = 'cont3'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
