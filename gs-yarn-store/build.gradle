buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.0.RC2")
    }
}

configure(rootProject) {
    task clean {
        doLast {
            ant.delete(dir: "build")
        }
    }
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'
    version =  '0.1.0'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-milestone" }
    }

    dependencies {
        compile("org.springframework.data:spring-yarn-boot:2.1.0.BUILD-SNAPSHOT")
    }

    jar {
        baseName = 'gs-yarn-store'
    }

    task copyJars(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/build/libs"
        include "**/*.jar"
    }
    build.doLast {
        copyJars.execute()
    }

}

project('client') {
    eclipse.project.name = 'gs-yarn-store-client'
    dependencies {
        compile("org.springframework.data:spring-yarn-boot-cli:2.1.0.BUILD-SNAPSHOT")
    }
    jar {
        appendix = 'client'
    }
}

project('appmaster') {
    eclipse.project.name = 'gs-yarn-store-appmaster'
    configurations.all {
        exclude group: 'javax.servlet', module: 'servlet-api', version: '2.5'
    }
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:1.2.0.RC2") {
            exclude group:'ch.qos.logback'
        }
        compile("org.springframework.boot:spring-boot-starter-actuator:1.2.0.RC2") {
            exclude group:'ch.qos.logback'
        }
        runtime("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")
    }
    jar {
        appendix = 'appmaster'
    }
}

project('container') {
    eclipse.project.name = 'gs-yarn-store-container'
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:1.2.0.RC2") {
            exclude group:'ch.qos.logback'
        }
        compile("org.springframework.boot:spring-boot-starter-actuator:1.2.0.RC2") {
            exclude group:'ch.qos.logback'
        }
        compile("org.springframework.data:spring-data-hadoop-store:2.1.0.BUILD-SNAPSHOT")
    }
    jar {
        appendix = 'container'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
